plugins {
    id 'com.jfrog.bintray' version '1.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id 'com.palantir.git-version' version '0.3.0'

    id 'com.palantir.baseline-checkstyle' version '0.2.2'
    id 'com.palantir.baseline-eclipse' version '0.2.2'
    id 'com.palantir.baseline-findbugs' version '0.2.2'
    id 'com.palantir.baseline-idea' version '0.2.2'

    id 'jacoco'
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = 1.7

group 'com.palantir.ri'
version gitVersion()

repositories {
    jcenter()
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier 'sources'
            }
            artifact(javadocJar) {
                classifier 'javadoc'
            }
        }
    }
}

tasks.check.dependsOn(javadoc)
tasks.eclipse.dependsOn(tasks.cleanEclipse)

jacocoTestReport.reports.xml.enabled true

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.coveralls {
    onlyIf { System.getenv('CI') }
    dependsOn tasks.jacocoTestReport
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'releases'
        name = 'resource-identifier'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['bintray']
    }
}

tasks.bintrayUpload.onlyIf {
    System.getenv('TRAVIS_TAG') && project.version ==~ /\d+\.\d+\.\d+/
}

tasks.bintrayUpload.dependsOn { generatePomFileForBintrayPublication }
tasks.bintrayUpload.dependsOn { sourceJar }
tasks.bintrayUpload.dependsOn { build }

